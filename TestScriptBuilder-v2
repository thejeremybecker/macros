Sub CreateTestScripts()
' Create workbooks from table using a template worksheet

' For each reference (row) in table
' -- create copy of Master sheet
' -- change sheet name to reference
' -- write reference to reference cell (VLOOKUP formulas will then populate sheet with row values)
' -- copy/paste sheet as values to remove formulas
' -- save sheet as new workbook
' -- delete sheet from macro workbook

Dim wb As Workbook              'Matrix workbook
Dim wsMaster As Worksheet       'Master sheet
Dim wsMasterIndex As Integer    'Master sheet index
Dim rMatrixKey As Range         'Source table range
Dim rMatrix As Range            'Source table
Dim oKey As String              'Reference cell in output sheet (from table column B)
Dim ows As Worksheet            'Output worksheet
Dim owb As Workbook             'Output workbook
Dim oID As String               'Reference (from table, must be unique)
Dim oPath As String             'Output path (from current directory)

Set wb = ActiveWorkbook
Set wsMaster = wb.Worksheets("Master")
wsMasterIndex = 3
Set rMatrix = Sheets("Table").Range("A2") 'this is the first cell in the source range
Set rMatrix = Range(rMatrix, rMatrix.End(xlDown))
Application.ScreenUpdating = False
For Each rMatrixKey In rMatrix
    If rMatrixKey.EntireRow.Hidden = False Then 'only includes non-hidden rows
        oID = rMatrixKey.Offset(, 0).Value
        oKey = rMatrixKey.Offset(, 1).Value
        oPath = ""
        wsMaster.Copy ActiveWorkbook.Sheets(Sheets.Count)
        ActiveWorkbook.Worksheets("Master (2)").Name = oID
        Set ows = Sheets(wsMasterIndex)
        ows.Range(oKey) = oID
        With ows.UsedRange
            .Cells.Copy
            .Cells.PasteSpecial xlPasteValues
            .Cells(1).Select
        End With
        Application.CutCopyMode = False
        oPath = ActiveWorkbook.Path & oPath
        MyMkDir oPath
        ows.Copy
        Set owb = ActiveWorkbook
        With owb
            .SaveAs oPath & "\" & oID, FileFormat:=51
            .Close False
        End With
        Application.DisplayAlerts = False
        ows.Delete
        Application.DisplayAlerts = True
    End If
Next rMatrixKey
Application.ScreenUpdating = True
MsgBox "All done! Workbooks are located in " & oPath
End Sub

Public Sub MyMkDir(sPath As String) 'creates directories with nested subfolders
' Source: https://www.devhut.net/2011/09/15/vba-create-directory-structurecreate-multiple-directories/
    Dim iStart          As Integer
    Dim aDirs           As Variant
    Dim sCurDir         As String
    Dim i               As Integer
 
    If sPath <> "" Then
        aDirs = Split(sPath, "\")
        If Left(sPath, 2) = "\\" Then
            iStart = 3
        Else
            iStart = 1
        End If
        sCurDir = Left(sPath, InStr(iStart, sPath, "\"))
        For i = iStart To UBound(aDirs)
            sCurDir = sCurDir & aDirs(i) & "\"
            If Dir(sCurDir, vbDirectory) = vbNullString Then
                MkDir sCurDir
            End If
        Next i
    End If
End Sub
